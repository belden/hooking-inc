#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;

my @samples = (
	q{cat -n lib/foo.pm},
	q{perl -Mfoo -e 'foo->INC'},
	q{perl -Mfoo -e 'main->INC'},
	q{vim -c 'set nu' lib/Fink.pm},
	q{perl -MFink -Mstrict -Mwarnings -MData::Dumper -e 1},
	q{perl -MIndented -Mstrict -Mwarnings -MData::Dumper -e 1},
	q{vim -c 'set nu' bin/toy1},
	q{bin/toy1 | egrep -i 'code|somewhere|elsewhere' | grep -v blyman},
	q{perl -MKingOfTheINC bin/toy1 | egrep -i 'code|somewhere|elsewhere' | grep -v blyman},
	q{perl -MAlwaysAtTail -e 'use lib "/tmp"; use something_not_here;'},
	q{vim -c 'set nu' lib/GenerateCode.pm},
	q{perl -MGenerateCode -e 'use lib "/tmp"; use something_not_here;'},
	q{vim -c 'set nu' lib/Peeker.pm},
	q{perl -MPeeker -MDBIx::Class -e 1 | less},
);

GetOptions(
	'reset' => \(my $do_reset = 0),
	'like=s' => \(my $search),
);

if ($do_reset) {
	open my $fh, '+>', '.sample_counter';
	print $fh "-1\n";
	close $fh;
	exec $0;
	die "exec failed: wtf?! $!\n";
}

if ($search) {
	my ($match) = grep { $_ =~ /$search/ } @samples;
	to_xclip($match) if $match;
} else {
	my $index = sample_number();
	my $desired = $samples[$index];
	die "no samples left!\n" unless $desired;
	to_xclip($desired, $index);
	bump_sample();
}

sub to_xclip {
	my ($sample, $index) = @_;
	open my $xclip, '| xclip' or die "fork xclip: $!\n";

	my $recurse = (defined $index && $samples[$index + 1])
		? "                                    ;sample"
		: '';

	my $newline = $sample =~ m{\| less} ? '' : "\n";
	print $xclip "$sample$recurse$newline";
	close $xclip;
}

sub bump_sample {
	my $number = sample_number();
	open my $fh, '+>', '.sample_counter';
	print $fh $number + 1, "\n";
	close $fh;
}

sub sample_number {
	local @ARGV = ('.sample_counter');
	my $number = <>;
	$number //= 0;
	chomp $number;
	return $number;
}
